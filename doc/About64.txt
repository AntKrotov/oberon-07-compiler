        Компилятор языка программирования Oberon-07/16 для Windows64.
------------------------------------------------------------------------------

        Параметры командной строки

  Вход - текстовые файлы модулей с расширением ".ob07", кодировка ANSI или
UTF-8 с BOM-сигнатурой.
  Выход - ассемблерный листинг (fasm v1.72).
  Параметры:
  1) имя главного модуля
  2) имя результирующего файла (.asm)
  3) тип приложения
      "console64" - Windows64 console
      "gui64" - Windows64 GUI
      "dll64" - Windows64 DLL
  4) необязательные параметры-ключи
      -nochk <"ptibcwra"> отключить проверки при выполнении

      параметр -nochk задается в виде строки из символов:
      "p" - указатели
      "t" - типы
      "i" - индексы
      "b" - неявное приведение INTEGER к BYTE
      "c" - диапазон аргумента функции CHR
      "w" - диапазон аргумента функции WCHR
      "r" - эквивалентно "bcw"
      "a" - все проверки

      Порядок символов может быть любым. Наличие в строке того или иного
      символа отключает соответствующую проверку.

      Например: -nochk it - отключить проверку индексов и охрану типа.
      -nochk a - отключить все отключаемые проверки.

  Например:

  Compiler.exe "C:\example.ob07" "C:\example.asm" console64
  Compiler.exe "C:\example.ob07" "C:\example.asm" dll64 -nochk pti

  В случае успешной компиляции, компилятор передает код завершения 0, иначе 1.

------------------------------------------------------------------------------
        Отличия от оригинала

1.      Расширен псевдомодуль SYSTEM
2.      В идентификаторах допускается символ "_"
3.      Добавлены системные флаги
4.      Усовершенствован оператор CASE (добавлены константные выражения в
        метках вариантов и необязательная ветка ELSE)
5.      Расширен набор стандартных процедур
6.      Семантика охраны/проверки типа уточнена для нулевого указателя
7.      Семантика DIV и MOD уточнена для отрицательных чисел
8.      Добавлены однострочные комментарии (начинаются с пары символов "//")
9.      Разрешено наследование от типа-указателя
10.     Добавлен синтаксис для импорта процедур из внешних библиотек
11.     "Строки" можно заключать также в одиночные кавычки: 'строка'
12.     Добавлен тип WCHAR

------------------------------------------------------------------------------
        Особенности реализации

1.      Основные типы

          Тип              Диапазон значений                      Размер, байт

        INTEGER       -9223372036854775808 .. 9223372036854775807       8
        REAL          4.94E-324 .. 1.70E+308                            8
        CHAR          символ ASCII (0X .. 0FFX)                         1
        BOOLEAN       FALSE, TRUE                                       1
        SET           множество из целых чисел {0 .. 63}                8
        BYTE          0 .. 255                                          1
        WCHAR         символ юникода (0X .. 0FFFFX)                     2

2.      Максимальная длина идентификаторов - 1024 символов
3.      Максимальная длина строковых констант - 1024 символов (UTF-8)
4.      Максимальная размерность открытых массивов - 5
5.      Процедура NEW заполняет нулями выделенный блок памяти
6.      Глобальные и локальные переменные инициализируются нулями
7.      В отличие от многих Oberon-реализаций, сборщик мусора и динамическая
        модульность отсутствуют
8.      Тип BYTE в выражениях всегда приводится к INTEGER
9.      Контроль переполнения значений выражений не производится
10.     Ошибки времени выполнения:

        - ASSERT(x), при x = FALSE
        - разыменование нулевого указателя
        - целочисленное деление на 0
        - вызов процедуры через процедурную переменную с нулевым значением
        - ошибка охраны типа
        - нарушение границ массива
        - непредусмотренное значение выражения в операторе CASE
        - ошибка копирования массивов v := x, если LEN(v) < LEN(x)
        - неявное приведение x:INTEGER к v:BYTE, если (x < 0) OR (x > 255)
        - CHR(x), если (x < 0) OR (x > 255)
        - WCHR(x), если (x < 0) OR (x > 65535)

------------------------------------------------------------------------------
        Псевдомодуль SYSTEM

  Псевдомодуль SYSTEM содержит низкоуровневые и небезопасные процедуры,
ошибки при использовании процедур псевдомодуля SYSTEM могут привести к
повреждению данных времени выполнения и аварийному завершению программы.

        PROCEDURE ADR(v: любой тип): INTEGER
                v - переменная или процедура;
                возвращает адрес v

        PROCEDURE SADR(x: строковая константа (CHAR UTF-8)): INTEGER
                возвращает адрес x

        PROCEDURE WSADR(x: строковая константа (WCHAR)): INTEGER
                возвращает адрес x

        PROCEDURE SIZE(T): INTEGER
                возвращает размер типа T

        PROCEDURE TYPEID(T): INTEGER
                T - тип-запись или тип-указатель,
                возвращает номер типа в таблице типов-записей

        PROCEDURE INF(): REAL
                возвращает специальное вещественное значение "бесконечность"

        PROCEDURE GET(a: INTEGER;
                VAR v: любой основной тип, PROCEDURE, POINTER)
                v := Память[a]

        PROCEDURE PUT(a: INTEGER; x: любой основной тип, PROCEDURE, POINTER)
                Память[a] := x;
                Если x: BYTE или x: WCHAR, то значение x будет расширено
                до 64 бит, для записи байтов использовать SYSTEM.PUT8,
                для WCHAR -- SYSTEM.PUT16

        PROCEDURE PUT8(a: INTEGER; x: INTEGER, BYTE, CHAR, WCHAR)
                Память[a] := младший байт (x)

        PROCEDURE PUT16(a: INTEGER; x: INTEGER, BYTE, CHAR, WCHAR)
                Память[a] := младшие 16 бит (x)

        PROCEDURE PUT32(a: INTEGER; x: INTEGER, BYTE, CHAR, WCHAR)
                Память[a] := младшие 32 бит (x)

        PROCEDURE MOVE(Source, Dest, n: INTEGER)
                Копирует n байт памяти из Source в Dest,
                области Source и Dest не должны перекрываться

        PROCEDURE COPY(VAR Source: любой тип; VAR Dest: любой тип; n: INTEGER)
                Копирует n байт памяти из Source в Dest.
                Эквивалентно
                SYSTEM.MOVE(SYSTEM.ADR(Source), SYSTEM.ADR(Dest), n)

        PROCEDURE CODE(byte1, byte2,... : BYTE)
                Вставка машинного кода,
                byte1, byte2 ... - константы в диапазоне 0..255,
                например:    
            
                SYSTEM.CODE(048H,08BH,045H,010H) (* mov rax,qword[rbp+16] *)

  Также в модуле SYSTEM определены типы CARD16 (2 байта) и CARD32 (4 байта).
Для типов CARD16 и CARD32 не допускаются никакие явные операции, за
исключением присваивания.

  Функции псевдомодуля SYSTEM нельзя использовать в константных выражениях.

------------------------------------------------------------------------------
        Системные флаги

  При объявлении процедурных типов и глобальных процедур, после ключевого
слова PROCEDURE может быть указан флаг соглашения вызова: [win64], [call64].
Например:

        PROCEDURE [call64] MyProc (x, y, z: INTEGER): INTEGER;

  Если указан флаг [win64], то принимается соглашение call64 и
процедуру-функцию можно вызвать как собственно процедуру, вне выражения.
  Если флаг не указан, то принимается внутреннее соглашение вызовов. [win64] и
[call64] используются для связи с операционной системой и внешними
приложениями.
  При объявлении типов-записей, после ключевого слова RECORD может быть
указан флаг [noalign]. Флаг [noalign] означает отсутствие выравнивания полей
записи. Записи с системным флагом не могут иметь базовый тип и не могут быть
базовыми типами для других записей.
  Для использования системных флагов, требуется импортировать SYSTEM.

------------------------------------------------------------------------------
        Оператор CASE

  Синтаксис оператора CASE:

        CaseStatement =
                CASE Expression OF Сase {"|" Сase}
                        [ELSE StatementSequence] END.
        Case = [CaseLabelList ":" StatementSequence].
        CaseLabelList = CaseLabels {"," CaseLabels}.
        CaseLabels = ConstExpression [".." ConstExpression].

  Например:

        CASE x OF
        |-1:    DoSomething1
        | 1:    DoSomething2
        | 0:    DoSomething3
        ELSE
                DoSomething4
        END

  В метках вариантов можно использовать константные выражения, ветка ELSE
необязательна. Если не выполнен ни один вариант и ELSE отсутствует, то
программа прерывается с ошибкой времени выполнения.

------------------------------------------------------------------------------
        Тип WCHAR

  Тип WCHAR добавлен в язык для удобной поддежки юникода. Для типов WCHAR и
ARRAY OF WCHAR допускаются все те же операции, как для типов CHAR и
ARRAY OF CHAR, за исключением встроенной процедуры CHR, которая возвращает
только тип CHAR. Для получения значения типа WCHAR, следует использовать
процедуру WCHR вместо CHR. Для правильной работы с типом, необходимо сохранять
исходный код в кодировке UTF-8 c BOM.

------------------------------------------------------------------------------
        Проверка и охрана типа нулевого указателя

  Оригинальное сообщение о языке не определяет поведение программы при
выполнении охраны p(T) и проверки типа p IS T при p = NIL. Во многих
Oberon-реализациях выполнение такой операции приводит к ошибке времени
выполнения. В данной реализации охрана типа нулевого указателя не приводит к
ошибке, а проверка типа дает результат FALSE. В ряде случаев это позволяет
значительно сократить частоту применения охраны типа.

------------------------------------------------------------------------------
        Дополнительные стандартные процедуры

        DISPOSE (VAR v: любой_указатель)
                Освобождает память, выделенную процедурой NEW для
                динамической переменной v^, и присваивает переменной v
                значение NIL.

        COPY (x: ARRAY OF CHAR/WCHAR; VAR v: ARRAY OF CHAR/WCHAR);
                v := x;
                Если LEN(v) < LEN(x), то строка x будет скопирована
                не полностью

        LSR (x, n: INTEGER): INTEGER
                Логический сдвиг x на n бит вправо.

        MIN (a, b: INTEGER): INTEGER
                Минимум из двух значений.

        MAX (a, b: INTEGER): INTEGER
                Максимум из двух значений.

        BITS (x: INTEGER): SET
                Интерпретирует x как значение типа SET.
                Выполняется на этапе компиляции.

        LENGTH (s: ARRAY OF CHAR/WCHAR): INTEGER
                Длина 0X-завершенной строки s, без учета символа 0X.
                Если символ 0X отсутствует, функция возвращает длину
                массива s. s не может быть константой.

        WCHR (n: INTEGER): WCHAR
                Преобразование типа, аналогично CHR(n: INTEGER): CHAR

------------------------------------------------------------------------------
        DIV и MOD

         x         y      x DIV y   x MOD y

         5         3         1         2
        -5         3        -2         1
         5        -3        -2        -1
        -5        -3         1        -2

------------------------------------------------------------------------------
        Импортированные процедуры

  Синтаксис импорта:

  PROCEDURE [callconv, "library", "function"] proc_name (FormalParam): Type;

  - callconv -- соглашение вызова (call64, win64)
  - "library" -- имя файла динамической библиотеки
  - "function" -- имя импортируемой процедуры

  например:

  PROCEDURE [win64, "kernel32.dll", "ExitProcess"] exit (code: INTEGER);


  В конце объявления может быть добавлено (необязательно) "END proc_name;"

  Объявления импортированных процедур должны располагаться в глобальной
  области видимости модуля после объявления переменных, вместе с объявлением
  "обычных" процедур, от которых импортированные отличаются только отсутствием
  тела процедуры. В остальном, к таким процедурам применимы те же правила:
  их можно вызвать, присвоить процедурной переменной или получить адрес.

  Так как импортированная процедура всегда имеет явное указание соглашения
  вызова, то совместимый процедурный тип тоже должен быть объявлен с указанием
  соглашения вызова:

  VAR
      ExitProcess: PROCEDURE [win64] (code: INTEGER);

------------------------------------------------------------------------------
        Скрытые параметры процедур

  Некоторые процедуры могут иметь скрытые параметры, они отсутствуют в списке
формальных параметров, но учитываются компилятором при трансляции вызовов.
Это возможно в следующих случаях:

1.      Процедура имеет формальный параметр открытый массив:
                PROCEDURE Proc (x: ARRAY OF ARRAY OF REAL);
        Вызов транслируется так:
                Proc(LEN(x), LEN(x[0]), SYSTEM.ADR(x))
2.      Процедура имеет формальный параметр-переменную типа RECORD:
                PROCEDURE Proc (VAR x: Rec);
        Вызов транслируется так:
                Proc(SYSTEM.TYPEID(Rec), SYSTEM.ADR(x))

------------------------------------------------------------------------------
        Модуль RTL

  Все программы неявно используют модуль RTL. Компилятор транслирует
некоторые операции (проверка и охрана типа, сравнение строк, сообщения об
ошибках времени выполнения и др.) как вызовы процедур этого модуля. Не
следует явно вызывать эти процедуры, за исключением процедуры SetDll,
если приложение компилируется как Windows DLL:

        PROCEDURE SetDll
            (process_detach, thread_detach, thread_attach: DLL_ENTRY);
        где TYPE DLL_ENTRY =
            PROCEDURE [stdcall] (hinstDLL, fdwReason, lpvReserved: INTEGER);

SetDll назначает процедуры process_detach, thread_detach, thread_attach
вызываемыми при
- выгрузке dll-библиотеки (process_detach)
- создании нового потока (thread_attach)
- уничтожении потока (thread_detach)

Для прочих типов приложений, вызов процедуры SetDll не влияет на
поведение программы.

  Сообщения об ошибках времени выполнения выводятся в диалоговых окнах
Windows.

------------------------------------------------------------------------------
        Модуль API

  Существуют несколько реализаций модуля API: для Windows64, Windows32 и для
KolibriOS. Как и модуль RTL, модуль API не предназначен для прямого
использования. Он обеспечивает кроссплатформенность компилятора.

------------------------------------------------------------------------------
        Генерация исполняемых файлов DLL

  Разрешается экспортировать только процедуры. Для этого, процедура должна
находиться в главном модуле программы, ее имя должно быть отмечено символом
экспорта ("*") и должно быть указано соглашение вызова [win64] или [call64].