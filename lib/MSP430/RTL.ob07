(*
    BSD 2-Clause License

    Copyright (c) 2019, Anton Krotov
    All rights reserved.
*)

MODULE RTL;

IMPORT SYSTEM;


CONST

    BP = 15;

    ram_adr = 200H;
    iv_adr  = 0FFC0H;

    sp_adr         = iv_adr - 2;
    empty_proc_adr = sp_adr - 2;
    bits_adr       = empty_proc_adr - 272;
    bits_offs_adr  = bits_adr - 32;
    types_adr      = bits_offs_adr - 2;

    trap_adr = ram_adr;
    int_adr  = trap_adr + 2;


TYPE

    TRAPPROC = PROCEDURE (module, err: INTEGER);

    INTPROC = PROCEDURE (priority, ptr: INTEGER);


PROCEDURE _lsl* (n, x: INTEGER);
BEGIN
    SYSTEM.CODE(
    4015H + BP * 100H, 4,  (*  MOV  4(BP), R5; R5 <- n  *)
    4014H + BP * 100H, 6,  (*  MOV  6(BP), R4; R4 <- x  *)
    0F035H, 15,            (*  AND  #15, R5             *)
    2400H + 3,             (*  JZ   L1                  *)
                           (*  L2                       *)
    5404H,                 (*  ADD  R4, R4              *)
    8315H,                 (*  SUB  #1, R5              *)
    2000H + 400H - 3       (*  JNZ  L2                  *)
                           (*  L1                       *)
    )
END _lsl;


PROCEDURE _asr* (n, x: INTEGER);
BEGIN
    SYSTEM.CODE(
    4015H + BP * 100H, 4,  (*  MOV  4(BP), R5; R5 <- n  *)
    4014H + BP * 100H, 6,  (*  MOV  6(BP), R4; R4 <- x  *)
    0F035H, 15,            (*  AND  #15, R5             *)
    2400H + 3,             (*  JZ   L1                  *)
                           (*  L2                       *)
    1104H,                 (*  RRA  R4                  *)
    8315H,                 (*  SUB  #1, R5              *)
    2000H + 400H - 3       (*  JNZ  L2                  *)
                           (*  L1                       *)
    )
END _asr;


PROCEDURE _ror* (n, x: INTEGER);
BEGIN
    SYSTEM.CODE(
    4015H + BP * 100H, 4,  (*  MOV  4(BP), R5; R5 <- n  *)
    4014H + BP * 100H, 6,  (*  MOV  6(BP), R4; R4 <- x  *)
    0F035H, 15,            (*  AND  #15, R5             *)
    2400H + 5,             (*  JZ   L1                  *)
    4406H,                 (*  MOV  R4, R6              *)
                           (*  L2                       *)
    1006H,                 (*  RRC  R6                  *)
    1004H,                 (*  RRC  R4                  *)
    8315H,                 (*  SUB  #1, R5              *)
    2000H + 400H - 4       (*  JNZ  L2                  *)
                           (*  L1                       *)
    )
END _ror;


PROCEDURE _lsr* (n, x: INTEGER);
BEGIN
    SYSTEM.CODE(
    4015H + BP * 100H, 4,  (*  MOV  4(BP), R5; R5 <- n  *)
    4014H + BP * 100H, 6,  (*  MOV  6(BP), R4; R4 <- x  *)
    0F035H, 15,            (*  AND  #15, R5             *)
    2400H + 4,             (*  JZ   L1                  *)
                           (*  L2                       *)
    0C312H,                (*  BIC  #1, SR              *)
    1004H,                 (*  RRC  R4                  *)
    8315H,                 (*  SUB  #1, R5              *)
    2000H + 400H - 4       (*  JNZ  L2                  *)
                           (*  L1                       *)
    )
END _lsr;


PROCEDURE _mul* (a, b: INTEGER);
BEGIN
    SYSTEM.CODE(
    4015H + BP * 100H, 4,  (*  MOV  4(BP), R5; R5 <- a  *)
    4016H + BP * 100H, 6,  (*  MOV  6(BP), R6; R6 <- b  *)
    4304H,                 (*  MOV  #0, R4; res := 0    *)
    9306H,                 (*  CMP  #0, R6              *)
    2400H + 7,             (*  JZ   L1                  *)
                           (*  L2                       *)
    0B316H,                (*  BIT  #1, R6              *)
    2400H + 1,             (*  JZ   L3                  *)
    5504H,                 (*  ADD  R5, R4              *)
                           (*  L3                       *)
    5505H,                 (*  ADD  R5, R5              *)
    0C312H,                (*  BIC  #1, SR              *)
    1006H,                 (*  RRC  R6                  *)
    2000H + 400H - 7       (*  JNZ  L2                  *)
                           (*  L1                       *)
    )
END _mul;


PROCEDURE _move* (bytes, dest, source: INTEGER);
BEGIN
    SYSTEM.CODE(
    4016H + BP * 100H, 4,  (*  MOV    4(BP), R6; R6 <- bytes   *)
    4017H + BP * 100H, 6,  (*  MOV    6(BP), R7; R7 <- dest    *)
    4015H + BP * 100H, 8,  (*  MOV    8(BP), R5; R5 <- source  *)
    9306H,                 (*  CMP    #0, R6                   *)
    3800H + 6,             (*  JL     L1                       *)
    2400H + 5,             (*  JZ     L1                       *)
                           (*  L2                              *)
    45F7H, 0,              (*  MOV.B  @R5+, 0(R7)              *)
    5317H,                 (*  ADD    #1, R7                   *)
    8316H,                 (*  SUB    #1, R6                   *)
    2000H + 400H - 5       (*  JNZ    L2                       *)
                           (*  L1                              *)
    )
END _move;


PROCEDURE _arrcpy* (base_size, len_dst, dst, len_src, src: INTEGER): BOOLEAN;
VAR
    res: BOOLEAN;

BEGIN
    IF len_src > len_dst THEN
        res := FALSE
    ELSE
        _move(len_src * base_size, dst, src);
        res := TRUE
    END

    RETURN res
END _arrcpy;


PROCEDURE _strcpy* (len_dst, dst, len_src, src: INTEGER);
BEGIN
    _move(MIN(len_dst, len_src), dst, src)
END _strcpy;


PROCEDURE _strcpy2* (len_src, src, len_dst, dst: INTEGER);
BEGIN
    _move(MIN(len_dst, len_src), dst, src)
END _strcpy2;


PROCEDURE _rot* (VAR A: ARRAY OF INTEGER);
VAR
    i, n, k: INTEGER;

BEGIN

    k := LEN(A) - 1;
    n := A[0];
    i := 0;
    WHILE i < k DO
        A[i] := A[i + 1];
        INC(i)
    END;
    A[k] := n

END _rot;


PROCEDURE _set* (b, a: INTEGER);
BEGIN
    SYSTEM.CODE(
    4014H + BP * 100H, 4,  (*  MOV  4(BP), R4; R4 <- b  *)
    4015H + BP * 100H, 6,  (*  MOV  6(BP), R5; R5 <- a  *)
    9504H,                 (*  CMP  R5, R4              *)
    3800H + 25,            (*  JL   L1                  *)
    9035H, 16,             (*  CMP  #16, R5             *)
    3400H + 22,            (*  JGE  L1                  *)
    9304H,                 (*  CMP  #0, R4              *)
    3800H + 20,            (*  JL   L1                  *)
    9034H, 16,             (*  CMP  #16, R4             *)
    3800H + 2,             (*  JL   L2                  *)
    4034H, 15,             (*  MOV  #15, R4             *)
                           (*  L2:                      *)
    9305H,                 (*  CMP  #0, R5              *)
    3400H + 1,             (*  JGE  L3                  *)
    4305H,                 (*  MOV  #0, R5              *)
                           (*  L3:                      *)
    8504H,                 (*  SUB  R5, R4              *)
    5404H,                 (*  ADD  R4, R4              *)
    5034H, bits_offs_adr,  (*  ADD  bits_offs_adr, R4   *)
    4424H,                 (*  MOV  @R4, R4             *)
    5505H,                 (*  ADD  R5, R5              *)
    5405H,                 (*  ADD  R4, R5              *)
    5035H, bits_adr,       (*  ADD  bits_adr, R5        *)
    4524H,                 (*  MOV  @R5, R4             *)
    4130H + BP,            (*  MOV  @SP+, BP            *)
    4130H,                 (*  MOV  @SP+, PC            *)
                           (*  L1:                      *)
    4304H                  (*  MOV  #0, R4              *)
    )
END _set;


PROCEDURE _set1* (a: INTEGER);
BEGIN
    SYSTEM.CODE(
    4014H + BP * 100H, 4,  (*  MOV  4(BP), R4; R4 <- a  *)
    0B034H, 0FFF0H,        (*  BIT  #0FFF0H, R4         *)
    2000H + 6,             (*  JNZ  L1                  *)
    5404H,                 (*  ADD  R4, R4              *)
    5034H, bits_adr,       (*  ADD  bits_adr, R4        *)
    4424H,                 (*  MOV  @R4, R4             *)
    4130H + BP,            (*  MOV  @SP+, BP            *)
    4130H,                 (*  MOV  @SP+, PC            *)
                           (*  L1                       *)
    4304H                  (*  MOV  #0, R4              *)
    )
END _set1;


PROCEDURE _in2* (i, s: INTEGER): BOOLEAN;
BEGIN
    SYSTEM.GET(bits_adr + i * 2, i)
    RETURN BITS(i) * BITS(s) # {}
END _in2;


PROCEDURE _in* (s, i: INTEGER): BOOLEAN;
BEGIN
    IF BITS(i) * {4..15} = {} THEN
        SYSTEM.GET(bits_adr + i * 2, i);
        s := ORD(BITS(i) * BITS(s))
    ELSE
        s := 0
    END

    RETURN s # 0
END _in;


PROCEDURE _incl* (VAR s: SET; i: INTEGER);
BEGIN
    IF BITS(i) * {4..15} = {} THEN
        SYSTEM.GET(bits_adr + i * 2, i);
        s := s + BITS(i)
    END
END _incl;


PROCEDURE _excl* (VAR s: SET; i: INTEGER);
BEGIN
    IF BITS(i) * {4..15} = {} THEN
        SYSTEM.GET(bits_adr + i * 2, i);
        s := s - BITS(i)
    END
END _excl;


PROCEDURE _divmod* (b, a: INTEGER);
VAR
    res, c, r: INTEGER;

BEGIN
    res := 0;

    WHILE (a >= b) OR (a < 0) DO
        r := 2;
        c := b * 2;
        WHILE (c > 0) & (ABS(a) >= c) DO
            c := c * 2;
            r := r * 2
        END;

        IF a < 0 THEN
            INC(a, LSR(c, 1));
            DEC(res, LSR(r, 1))
        ELSE
            DEC(a, LSR(c, 1));
            INC(res, LSR(r, 1))
        END
    END;

    b := res;

    SYSTEM.CODE(
    4014H + BP * 100H, 4,  (*  MOV  4(BP), R4; R4 <- res  *)
    4015H + BP * 100H, 6,  (*  MOV  6(BP), R5; R5 <- mod  *)
    )
END _divmod;


PROCEDURE _length* (len, str: INTEGER): INTEGER;
BEGIN
    SYSTEM.CODE(
    4016H + BP * 100H, 4,  (*  MOV    4(BP), R6; R6 <- len  *)
    4017H + BP * 100H, 6,  (*  MOV    6(BP), R7; R7 <- str  *)
    4304H,                 (*  MOV    #0, R4; res := 0      *)
                           (*  L2:                          *)
    4775H,                 (*  MOV.B  @R7+, R5              *)
    9305H,                 (*  CMP    #0, R5                *)
    2400H + 3,             (*  JZ     L1                    *)
    5314H,                 (*  ADD    #1, R4                *)
    8316H,                 (*  SUB    #1, R6                *)
    2000H + 400H - 6,      (*  JNZ    L2                    *)
                           (*  L1:                          *)
    4130H + BP,            (*  MOV    @SP+, BP              *)
    4130H                  (*  MOV    @SP+, PC              *)
    )
    RETURN 0
END _length;


PROCEDURE strncmp (a, b, n: INTEGER): INTEGER;
VAR
    A, B: CHAR;

BEGIN
    WHILE n > 0 DO
        SYSTEM.GET(a, A); INC(a);
        SYSTEM.GET(b, B); INC(b);
        DEC(n);
        IF A # B THEN
            n := 0
        ELSIF A = 0X THEN
            n := 0
        END
    END
    RETURN ORD(A) - ORD(B)
END strncmp;


PROCEDURE _strcmp* (op, len2, str2, len1, str1: INTEGER): BOOLEAN;
VAR
    res:  INTEGER;
    bRes: BOOLEAN;

BEGIN

    res := strncmp(str1, str2, MIN(len1, len2));
    IF res = 0 THEN
        res := _length(len1, str1) - _length(len2, str2)
    END;

    CASE op OF
    |0: bRes := res =  0
    |1: bRes := res #  0
    |2: bRes := res <  0
    |3: bRes := res <= 0
    |4: bRes := res >  0
    |5: bRes := res >= 0
    END

    RETURN bRes
END _strcmp;


PROCEDURE _strcmp2* (op, len1, str1, len2, str2: INTEGER): BOOLEAN;
    RETURN _strcmp(op, len2, str2, len1, str1)
END _strcmp2;


PROCEDURE _is* (t0, p: INTEGER): BOOLEAN;
VAR
    t1: INTEGER;

BEGIN
    (* p IS t0 *)

    IF p # 0 THEN
        DEC(p, 2);
        SYSTEM.GET(p, t1);
        WHILE (t1 # 0) & (t1 # t0) DO
            SYSTEM.GET(types_adr - t1 * 2, t1)
        END
    ELSE
        t1 := -1
    END

    RETURN t1 = t0
END _is;


PROCEDURE _guard* (t0, p: INTEGER): BOOLEAN;
VAR
    t1:  INTEGER;

BEGIN
    (* p IS t0 *)

    SYSTEM.GET(p, p);
    IF p # 0 THEN
        DEC(p, 2);
        SYSTEM.GET(p, t1);
        WHILE (t1 # t0) & (t1 # 0) DO
            SYSTEM.GET(types_adr - t1 * 2, t1)
        END
    ELSE
        t1 := t0
    END

    RETURN t1 = t0
END _guard;


PROCEDURE _guardrec* (t0, t1: INTEGER): BOOLEAN;
BEGIN
    (* r:t1 IS t0 *)

    WHILE (t1 # 0) & (t1 # t0) DO
        SYSTEM.GET(types_adr - t1 * 2, t1)
    END

    RETURN t1 = t0
END _guardrec;


PROCEDURE _error* (module, err: INTEGER);
BEGIN
    SYSTEM.CODE(
    0C232H,               (*  BIC   #8, SR; DINT      *)
    4303H,                (*  MOV   R3, R3; NOP       *)
    4211H, sp_adr,        (*  MOV   sp_adr(SR), SP    *)
    1210H + BP, 6,        (*  PUSH  6(BP)             *)
    1210H + BP, 4,        (*  PUSH  4(BP)             *)
    1210H + BP, 2,        (*  PUSH  2(BP)             *)
    1210H + BP, 0,        (*  PUSH  0(BP)             *)
    4100H + BP,           (*  MOV   SP, BP            *)
    4214H, trap_adr,      (*  MOV   trap_adr(SR), R4  *)
    9304H,                (*  TST   R4                *)
    2400H + 5,            (*  JZ    L                 *)
    1210H + BP, 6,        (*  PUSH  6(BP)             *)
    1210H + BP, 4,        (*  PUSH  4(BP)             *)
    1284H,                (*  CALL  R4                *)
                          (*  L:                      *)
    0D032H, 10H           (*  BIS   #10H, SR; CPUOFF  *)
    )
END _error;


PROCEDURE SetTrap* (trap: TRAPPROC);
BEGIN
    SYSTEM.PUT(trap_adr, trap)
END SetTrap;


PROCEDURE SetInt* (int: INTPROC);
BEGIN
    SYSTEM.PUT(int_adr, int)
END SetInt;


END RTL.