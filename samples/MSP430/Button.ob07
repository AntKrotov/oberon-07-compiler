(*

Пример для LaunchPad MSP-EXP430G2 Rev1.5

  Мигает зеленый светодиод.
  При нажатии на кнопку P1.3, включается/выключается красный светодиод.

*)

MODULE Button;

IMPORT SYSTEM, RTL;


CONST

    REDLED   = {0};
    GREENLED = {6};
    BUTTON   = {3};

    P1OUT = 21H;
    P1DIR = 22H;
    P1IFG = 23H;
    P1IE  = 25H;
    P1REN = 27H;


PROCEDURE delay (n: INTEGER);
BEGIN
    WHILE n > 0 DO DEC(n) END
END delay;


PROCEDURE get_bit (mem: INTEGER; bit: SET): BOOLEAN;
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b)
    RETURN bit * BITS(b) # {}
END get_bit;


PROCEDURE set_bits (mem: INTEGER; bits: SET);
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b);
    SYSTEM.PUT8(mem, BITS(b) + bits)
END set_bits;


PROCEDURE clr_bits (mem: INTEGER; bits: SET);
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b);
    SYSTEM.PUT8(mem, BITS(b) - bits)
END clr_bits;


PROCEDURE inv_bits (mem: INTEGER; bits: SET);
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b);
    SYSTEM.PUT8(mem, BITS(b) / bits)
END inv_bits;


(*
обработчик прерываний

  priority - приоритет прерывания:

  адрес    приоритет
  0FFFEH     31
  0FFFCH     30
  0FFFAH     29
  ...
  0FFC0H      0


  ptr - указатель на структуру:

  смещение    значение
     0:     priority
    +2:     сохраненное значение регистра SR
    +4:     сохраненное значение регистра PC
*)
PROCEDURE int (priority, ptr: INTEGER);
BEGIN
    IF priority = 18 THEN               (* прерывание от порта P1 *)
        IF get_bit(P1IFG, BUTTON) THEN  (* нажата кнопка *)
            inv_bits(P1OUT, REDLED);    (* изменить состояние светодиода *)
            delay(30000);               (* задержка для отпускания кнопки *)
            clr_bits(P1IFG, BUTTON)     (* сбросить флаг прерывания *)
        END
    END
END int;


PROCEDURE main;
BEGIN
    (* инициализация спец. регистров *)
    SYSTEM.PUT8(P1DIR, REDLED + GREENLED);
    set_bits(P1REN, BUTTON);
    set_bits(P1OUT, BUTTON);
    set_bits(P1IE,  BUTTON);

    RTL.SetInt(int);     (* назначить обработчик прерываний *)
    SYSTEM.CODE(0D232H); (* BIS #8, SR; EINT; разрешить прерывания *)

    (* бесконечный цикл *)
    WHILE TRUE DO
        inv_bits(P1OUT, GREENLED); (* изменить состояние светодиода *)
        delay(30000)               (* задержка *)
    END
END main;


BEGIN
    main
END Button.