(*

Пример для LaunchPad MSP-EXP430G2 Rev1.5

  Мигает зеленый светодиод.
  При нажатии на кнопку P1.3, включается/выключается красный светодиод.

*)

MODULE Example1;

IMPORT SYSTEM, RTL;


CONST

    REDLED   = 0;
    GREENLED = 6;
    BUTTON   = 3;

    P1OUT = 21H;
    P1DIR = 22H;
    P1IFG = 23H;
    P1IE  = 25H;
    P1REN = 27H;


PROCEDURE delay (n: INTEGER);
BEGIN
    WHILE n > 0 DO DEC(n) END
END delay;


PROCEDURE get_bit (mem, bit: INTEGER): BOOLEAN;
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b)
    RETURN bit IN BITS(b)
END get_bit;


PROCEDURE set_bit (mem, bit: INTEGER);
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b);
    SYSTEM.PUT8(mem, BITS(b) + {bit})
END set_bit;


PROCEDURE clr_bit (mem, bit: INTEGER);
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b);
    SYSTEM.PUT8(mem, BITS(b) - {bit})
END clr_bit;


PROCEDURE inv_bit (mem, bit: INTEGER);
VAR
    b: BYTE;

BEGIN
    SYSTEM.GET(mem, b);
    SYSTEM.PUT8(mem, BITS(b) / {bit})
END inv_bit;


(*
обработчик прерываний

  priority - приоритет прерывания:

  адрес    приоритет
  0FFFEH     31
  0FFFCH     30
  0FFFAH     29
  ...
  0FFC0H      0


  ptr - указатель на структуру:

  смещение    значение
     0:     priority
    +2:     сохраненное значение регистра SR
    +4:     сохраненное значение регистра PC
*)
PROCEDURE int (priority, ptr: INTEGER);
BEGIN
    IF priority = 18 THEN  (* прерывание от порта P1 *)
        IF get_bit(P1IFG, BUTTON) THEN (* нажата кнопка *)
            inv_bit(P1OUT, REDLED);
            delay(30000);
            clr_bit(P1IFG, BUTTON)
        END
    END
END int;


PROCEDURE main;
BEGIN
    (* инициализация спец. регистров *)
    SYSTEM.PUT8(P1DIR, {REDLED, GREENLED});
    set_bit(P1REN, BUTTON);
    set_bit(P1OUT, BUTTON);
    set_bit(P1IE,  BUTTON);

    RTL.SetInt(int); (* назначить обработчик прерываний *)
    SYSTEM.CODE(0D232H); (* BIS #8, SR; EINT; разрешить прерывания *)

    WHILE TRUE DO
        inv_bit(P1OUT, GREENLED);
        delay(30000)
    END
END main;


BEGIN
    main
END Example1.